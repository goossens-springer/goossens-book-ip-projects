dnl Welcome to the Sphinx automated build system.
dnl try not to hurt yourself ;)

AC_INIT(configure.in)
AM_INIT_AUTOMAKE(sphinx2,0.3)
AM_CONFIG_HEADER(config.h)

CFLAGS=${CFLAGS:--g -O2 -Wall}

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

AC_TYPE_SIGNAL

hmmdata=`ls -1 $srcdir/model/hmm/6k | egrep -v '(Makefile|CVS)'`
hmmdata=`echo $hmmdata`

lmdata=`ls -1 $srcdir/model/lm/turtle/ | egrep -v '(Makefile|CVS)'`
lmdata=`echo $lmdata`

docdata=`ls -1 $srcdir/doc | egrep -v '(Makefile|CVS)'`
docdata=`echo $docdata`

AC_CANONICAL_HOST

ad_files=""
ad_libs=""

AC_ARG_WITH(alsa, [use ALSA library for sound I/O],
	    AC_CHECK_HEADER(sys/asoundlib.h,
		[ad_libs="-lasound"; ad_files="ad_alsa.lo";
		 ad_backend="AD_BACKEND_ALSA"],
		AC_ERROR(ALSA header file <sys/asoundlib.h> not found!)))

if test "x$ad_files" = "x"; then
	case $host in
		*-*-linux*)
			ad_files="ad_oss.lo"
			ad_backend="AD_BACKEND_OSS"
			;;
		# FIXME: isn't this the same OSS as on Linux?
		*-*-freebsd*|*-*-netbsd*|*-*-openbsd*)
			ad_files="ad_oss_bsd.lo mulaw_base.lo"
			ad_backend="AD_BACKEND_OSS_BSD"
			;;
		*-*-osf*)
			ad_files="ad_osf.lo mulaw_base.lo"
			ad_backend="AD_BACKEND_OSF"
			;;
		*-*-irix*)
			ad_files="ad_irix.lo mulaw_base.lo"
			ad_libs="-laudio"
			ad_backend="AD_BACKEND_IRIX"
			;;
		*-*-sunos4*|*-*-solaris*)
			ad_files="ad_sunos.lo audio_utils_sunos.lo"
			ad_backend="AD_BACKEND_SUNOS"
			;;
		*-*-*win32*)
			ad_files="play_win32.lo rec_win32.lo"
			ad_backend="AD_BACKEND_WIN32"
			;;
		*)
			ad_files="ad_base.lo"
			ad_backend="AD_BACKEND_NONE"
			AC_MSG_RESULT([No audio interface for host type $host.])
			;;
	esac
fi

AC_PATH_PROG(PERL,perl)
AC_PATH_PROG(CSH,csh)
AC_SUBST(hmmdata)
AC_SUBST(lmdata)
AC_SUBST(docdata)
AC_SUBST(ad_files)
AC_SUBST(ad_libs)
AC_SUBST(ad_backend)
AC_SUBST(LIBOBJS)
AC_SUBST(PERL)
AC_SUBST(CSH)

AM_PROG_LIBTOOL

AC_OUTPUT([
Makefile
include/Makefile
include/ad.h
src/Makefile
src/libsphinx2/Makefile
src/libsphinx2/include/Makefile
src/libsphinx2fe/Makefile
src/libsphinx2ad/Makefile
src/examples/Makefile
model/Makefile
model/hmm/Makefile
model/hmm/6k/Makefile
model/lm/Makefile
model/lm/turtle/Makefile
doc/Makefile
scripts/Makefile
scripts/allphone-test.csh
scripts/batch.csh
scripts/sphinx2-demo
scripts/sphinx2-simple
scripts/sphinx2-phone
scripts/sphinx2-test
scripts/stress2sphinx
scripts/sphinx2-align
])

chmod +x scripts/allphone-test.csh
chmod +x scripts/batch.csh
chmod +x scripts/sphinx2-demo
chmod +x scripts/sphinx2-simple
chmod +x scripts/sphinx2-test
chmod +x scripts/sphinx2-phone
chmod +x scripts/stress2sphinx
chmod +x scripts/sphinx2-align
